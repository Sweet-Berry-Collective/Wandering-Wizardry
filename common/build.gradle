plugins {
	id 'java'
	alias libs.plugins.vanilla.gradle
}

repositories {
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "Forge"
		url = "https://maven.minecraftforge.net/"
	}
}

minecraft {
	version(libs.versions.minecraft.get())
	accessWideners(file("src/main/resources/wwizardry.accesswidener"))
	runs {
		server()
		client()
	}
}

sourceSets {
	main {
		resources {
			srcDir("src/generated/resources")
		}
	}
}

def classify(module, classifier) {
	var m = module.get()
	return "${m.module.group}:${m.module.name}:${m.version}:$classifier"
}

dependencies {
	compileOnly libs.bundles.mixin
	compileOnly libs.bundles.include.common

	compileOnly classify(libs.emi.common, "api")
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 17
	dependsOn ":generateData"
}
